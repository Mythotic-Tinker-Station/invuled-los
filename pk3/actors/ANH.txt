ACTOR Annihilator 30111
{
Obituary "The Cyber Annihilator blew %o away with its rockets."
Health 7000
Radius 40
Height 110
Mass 1000
Speed 16
PainChance 10
MONSTER
+NOTARGET
+FLOORCLIP
+NORADIUSDMG
+BOSS
+MISSILEEVENMORE
SeeSound "monster/annsit"
PainSound "cyber/pain"
DeathSound "monster/anndth"
ActiveSound "cyber/active"
States
   {
   Spawn:
       ANNI AB 10 A_Look
       Loop
   See:
       ANNI A 3 A_Playsound ("monster/anhoof")
       ANNI ABBCC 3 A_Chase
       ANNI D 3 A_Playsound ("monster/anhoof")
       ANNI D 3 A_Chase
       Loop
   Missile:
       ANNI E 0 A_Jump (128,11)
       ANNI E 6 A_FaceTarget
       ANNI F 0 Bright A_CustomMissile ("Rocket2",40,27,0)
       ANNI F 12 Bright A_CustomMissile ("Rocket2",40,-27,0)
       ANNI E 12 A_FaceTarget
       ANNI F 0 Bright A_CustomMissile ("Rocket2",40,27,0)
       ANNI F 12 Bright A_CustomMissile ("Rocket2",40,-27,0)
       ANNI E 12 A_FaceTarget
       ANNI F 0 Bright A_CustomMissile ("Rocket2",40,27,0)
       ANNI F 12 Bright A_CustomMissile ("Rocket2",40,-27,0)
       ANNI E 0 A_Jump (128,1)
       Goto See
       ANNI E 6 A_FaceTarget
       ANNI F 0 Bright A_CustomMissile ("HomRocket1",40,27,0)
       ANNI F 12 Bright A_CustomMissile ("HomRocket1",40,-27,0)
       ANNI E 12 A_FaceTarget
       ANNI F 0 Bright A_CustomMissile ("HomRocket1",40,27,0)
       ANNI F 12 Bright A_CustomMissile ("HomRocket1",40,-27,0)
       ANNI E 12 A_FaceTarget
       ANNI F 0 Bright A_CustomMissile ("HomRocket1",40,27,0)
       ANNI F 12 Bright A_CustomMissile ("HomRocket1",40,-27,0)
       Goto See
   Pain:
       ANNI G 10 A_Pain
       Goto See
   Death:
       ANNI H 10 Bright
       ANNI I 10 Bright A_Scream
       ANNI JKL 10 Bright
       ANNI M 10 Bright A_NoBlocking
       ANNI NO 10 Bright
       ANNI P 30
       ANNI P -1 A_BossDeath
   }
}

actor Rocket2
{
  obituary "%o rode %k's rocket."
  radius 11
  height 8
  speed 20
  damage 20
  seesound "weapons/rocklf"
  deathsound "weapons/rocklx"
  PROJECTILE
  +RANDOMIZE
  +ROCKETTRAIL
  +THRUGHOST
  Decal Scorch
  states
  {
  Spawn:
    MISL A 1 bright
    loop
  Death:
    MISL B 0 bright A_SetTranslucent (0.67,1)
    MISL B 8 bright A_Explode
    MISL C 6 bright
    MISL D 4 bright
    stop
  }
}

ACTOR HomRocket1
{
   Radius 8
   Height 11
   Speed 30
   Damage 10
   PROJECTILE
   DeathSound "weapons/hrlexp"
   ExplosionDamage 64
   ExplosionRadius 96
   +SeekerMissile
   +Rockettrail
   +THRUGHOST
   SeeSound "weapons/hrlfir"
   Decal Scorch
   States
   {
   Spawn:
      HMIS A 4 Bright A_SeekerMissile (25,35)
      HMIS A 4 Bright
      HMIS A 2 Bright A_SeekerMissile (10,25)
      HMIS A 2 Bright
      goto Spawn+2
   Death:
      BAL3 C 0 Bright A_SetTranslucent (0.67,1)
      BAL3 C 8 Bright A_Explode
      BAL3 DE 5 Bright
      stop
   }
}

actor AnnihilatorSpawner : CustomMonsterInvasionSpot 11147
{
	//$Category invasion_spots
	//$Sprite ANNIA2C8
	DropItem Annihilator
}